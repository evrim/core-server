(in-package :core-server)

;; -------------------------------------------------------------------------
;; HTTP Client
;; -------------------------------------------------------------------------
;; Author: Evrim Ulu <evrim@core.gen.tr>
;; Last Update: August 2012

(defvar +http-cache+ (make-hash-table :test #'equal :synchronized t)
  "HTTP Cache Hash Table (url . entities)")

;; -------------------------------------------------------------------------
;; HTTP Client
;; -------------------------------------------------------------------------
(defcommand http ()
  ((method :host local :initform 'get)
   (url :host local :initform (error "Please provide :url"))
   (username :host local :initform nil)
   (password :host local :initform nil)
   (post-data :host local :initform nil)
   (debug-p :host local :initform nil :documentation "Enable debug statements")
   (parse-p :host local :initform t :documentation "Parse response entities")
   (cache-p :host local :initform nil :documentation "Cache?")
   (error-p :host local :initform t :documentation "Raise error?")   
   (request-headers :host local :initform nil)))

(defmethod http.debug ((self http) &rest args)
  (if (http.debug-p self)
      (format *standard-output* "HTTP:窿ア狎珞┅ㄤ彐礤翳镤梏麴蜥轶瀛弪蝻è箦戽梏麴蝈聃弩蝈箴镱箦ㄩㄨ趑甬弪蝻颦箦戽ㄥ蝌矧⑷栽郁狒躞狒躜立ㄨ趑瓠蝈箴镱箦篝狒躞泔溴蝈箴镱箦躜榄倔趄轭蟓躜飑┅┅ㄤ彐礤翳镤梏麴篌飙è箦戽梏麴┅篝蜷铉㈣趑痼躜楫筱桢礤蟓躜飑┅ㄤ彐礤翳镤梏麴徜洵聃弪è箦戽梏麴钺礤鲠祯濠箦翩躜楫聃弪殄蟓躜飑铗弼弪箦ㄣ镱ㄣ镱钺礤鲠祯濠铗弼弪箦躜楫聃弪殄蟓躜飑┅┅蟓躜飑ㄤ彐礤翳镤梏麴徜洵痫篝è箦戽梏麴钺礤鲠祯濠鏖翳箪雉痫篝溽翎箦戽箦翩痫篝溽翎ㄩ铛祆痫篝溽翎ㄦ矧磲铋两立ㄥ筱狃瀛狍躜钺礤ㄥ筱狃瀛狍躜鲠祯濠ㄦ矧磲铋力两立痫篝溽翎ㄥ筱狃瀛狍躜钺礤ㄥ筱狃瀛狍躜鲠祯濠┅┅ㄤ彐礤翳镤梏麴箦趱瓠躜è箦戽梏麴┅ㄩ篝蜷铉蟓躜飑箦翩蟓躜飑躜榭磲脲泔蝈篝蝈犴蟓躜飑┅┅蟓躜飑ㄤ彐礤翳镤梏麴泔铑邈è箦戽梏麴┅鏖翳箪雉躜飑箦戽戾舄è篝蝈犴ㄣ镱铄泗躜楫箦蝣弪躜飑矧躜楫痫螋躜飑ㄩㄨ趑甬篌飙箦戽创赴┅┅＋篌篝蝈犴ㄩㄨ趑甬篌飙箦戽磲脲泔蝈篝蝈犴ㄣ飓篌旌磲脲篌飙沆殄铘篝蝈犴箪雉鲠祯篝蝈犴Д篝蝈犴┅篝蝈犴┅篝蝈犴┅ㄤ彐礤翳镤梏麴磲脲蝈聃弩è箦戽梏麴┅鏖翳箪雉礤翳镤躜飑箦戽戾è玷扉篝ㄣ镱躞弪徵孱梏麴沆殄铘ㄣ镱ц矬ㄣ镱躜楫箦蝣弪躜飑躜楫痫螋躜飑┅ㄣ镱п沣屦扉篝┅┅ㄥㄩ蟓ю矬舡溽翎扉篝ㄣ镱с镱翦铘戾铉翳戾铉翳蟓ю矬舡溽翎┅ㄣ镱с镱翦铘豉疱扉篝⑨痧扉汜糸镱Ⅷ鼢鳝骘蝽躜戾钽镤邃┅┅┅磲脲梏麴蝈聃弩喉弭栾礤翳镤乎蜷躜鸿遽溴蝮ㄡ痧孱玷蟓蝈聃弩舡桢徜弪螬哄铘轸桢徜弪彖┅┅ㄤ彐礤翳镤梏麴箦钿蝈聃弩è箦戽梏麴篝蝈犴泔蝈驿轱篝蝈犴┅戾è蝈聃弩ㄨ趑甬磲脲蝈聃弩箦戽┅ㄣ桢汶痫轭舡篝蝈犴篝蝈犴ㄨ趑瓠蝈聃弩簟篝蝈犴蝈聃弩舂ㄩ蟓т邂蹒皓ㄤ弩泸殁蝈聃弩舂ㄡ麒孱蟓ю矬舡溽翎ㄨ趑甬溴怩箦戽ю矬舡溽翎蟓ю矬舡溽翎┅篝蜷铉篝蝈犴轸┅麒孱蟓т邂蹒皓ㄦ矧磲篝犷溽蜾秕麴豸⒁羼蹂篝秕麴豸怩骀弪湖アㄦ矧磲篝犷溽蜾秕麴豸アㄦ矧磲篝犷溽蜾秕麴豸立镢翦趔麸篝蜷铉箪雉鲠祯篝蝈犴Д黩轸瀛怩骀弪乎翩俯ㄦ矧磲篝犷溽蜾秕麴豸ア┅ㄣ镯黹舡篝蝈犴篝蝈犴ㄡ篌弪ㄣ躜蝈铘汨邈腽镩铘篝蝈犴癌蝈聃弩舂ㄤ彐疳蝮弪蝈徜弼弪翳轭缈ㄣㄡ沣磲脲徙沲眭灬麸衡翦┅ê镲ê豉疱镢翦艨悌ê泔祆邈徙悌ê蝈趱蝾徙悌ㄤ彐礤翳镤梏麴疳蝮瀛蝈箴镱箦è箦戽梏麴篝蝈犴泔蝈驿轱篝蝈犴┅戾舄è蝈箴镱箦ㄨ趑瓠蝈箴镱箦篝蝈犴┅ㄣ镱翦铘豉疱ㄨ趑瓠蝈箴镱箦珏舡泔铘孱舡豉疱蝈箴镱箦┅ㄣ镱翦铘戾铉翳ㄨ趑瓠蝈箴镱箦珏舡泔铘孱舡戾铉翳蝈箴镱箦┅篝蝈犴ㄣ镱è篝蜷铉汨躅脲洧ㄣ後ㄨ趑瓠蝈箴镱箦珏舡蝈箴镱箦桢徜弪蝈箴镱箦趄犷箧弪孱泔溟铉┅磲脲汨躅脲洵篝蝈犴篝蝈犴┅è犷泔铘孱舡戾铉翳窘泔铘孱舡戾铉翳癌磲脲怙躅溴洵篝蝈犴篝蝈犴泔铘孱舡戾铉翳┅篝蝈犴┅┅痱镧蝈箴镱箦箦翩ㄨ趑瓠蝈箴镱箦篝蝈犴蝈箴镱箦篝蝈犴ㄩ蟓т邂蹒皓ㄤ弩泸殁蝈箴镱箦┅ㄣ镱è铒ㄨ趑甬疳蝮瀛箦戽┅豉疱汜箦篝蝈犴è矧泔蝈汨躅脲洵篝蝈犴怙躅溴洵篝蝈犴戾è孱糸豉蝈徜弼弪翳轭缈篝蝈犴┅箦翩ㄨ趑瓠蝈箴镱箦孱糸糸弩蝈箴镱箦扉篝孱糸豉┅┅ㄥ蝌矧⒚犷铒蝈徜弼弪翳轭缈铒泔铘孱舡戾铉翳┅┅è羼蟓ы弭栾洎ц遽洎蝈箴镱箦è矧ㄡ钿篝蜷铉Ⅳ屮簪ㄣ狎泔铘孱舡豉疱┅篝蜷铉㈣繇膦ㄣ徜泔铘孱舡豉疱┅ㄡ钿篝蜷铉⑨痧扉汜糸镱ㄣ狎泔铘孱舡豉疱┅篝蜷铉⑨麸慝盱ㄣ徜泔铘孱舡豉疱┅┅戾è孱糸豉蝈徜篝蝈犴磲脲蝈灬邃盱篝蝈犴篝蝈犴┅┅ㄩ孱糸豉箦翩ㄨ趑瓠蝈箴镱箦孱糸糸弩蝈箴镱箦扉篝孱糸豉┅┅è犷矧篝蜷铉㈥狯狍泸轲簪ㄣ徜泔铘孱舡豉疱┅篝蜷铉㈥箫睥ㄣ徜泔铘孱舡豉疱┅矧篝蜷铉Ⅳ屮簪ㄣ狎泔铘孱舡豉疱┅篝蜷铉⑨痧扉汜糸镱ㄣ狎泔铘孱舡豉疱┅┅戾è孱糸豉牦镱篝蝈犴┅ㄩ孱糸豉箦翩ㄨ趑瓠蝈箴镱箦孱糸糸弩蝈箴镱箦扉篝孱糸豉┅┅豉疱汜箦篝蝈犴è矧泔蝈汨躅脲洵篝蝈犴怙躅溴洵篝蝈犴戾è孱糸豉蝈徜弼弪翳轭缈篝蝈犴┅箦翩ㄨ趑瓠蝈箴镱箦孱糸糸弩蝈箴镱箦扉篝孱糸豉┅┅ㄥ蝌矧⒚犷铒蝈徜弼弪翳轭缈铒泔铘孱舡戾铉翳┅┅┅┅ㄤ彐礤翳镤梏麴狨翳矧辁è箦戽梏麴豉疱舂疳蜥礤翦蝮ㄥ蝌矧⒄铙躔痫螋邃狨翳孱糸汜糸镱礤翳镤窿ㄣ镱豉疱疳蜥礤翦蝮┅ㄤ彐礤翳镤梏麴狨翳矧辁è箦戽梏麴豉疱ㄥ耢р狍殂┅疳蜥礤翦蝮鏖翳箪雉躞弪钺礤疳篌黠蜾箦戽戾è狨翳矧辁狒轱鏖翳泔蝈篝蝈犴ㄢ狍宥础ㄣ镱汜躞弪钺礤⒑疳篌黠蜾┅蝈趱蝾篝蝈犴螬┅痱镧箦戽箦翩蟓蝈聃弩舡桢徜弪螬扉篝ㄣ镱п豸栾蜷狒轱ㄣ镱р狍殂狨翳矧辁狒轱瞟┅┅┅ㄤ彐礤翳镤梏麴狨翳矧辁è箦戽梏麴豉疱ㄥ耢т殓弩舂疳蜥礤翦蝮ㄦ戾è珏舡疳蜥钺礤ㄣ潋ㄡ篌镢钺礤疳蜥礤翦蝮呼弩＇羼踽飑┅ㄣ犰悱狨翳蝈箴镱箦蝈犰躞弪钺礤疳篌黠蜾躜铒钽钽耧泐镱沐戾è礤翳镤簌礅镬钺礤蟓ы弭栾洎┅戾è栳礓ㄣ镱汜躞弪钺礤⒑蝈犰⒑疳篌黠蜾┅ㄨ岵礓ㄣ镱汜礤翳镤⒑躜椹┅礓ㄣ镱汜栳⒑铒钽⒑钽⒑泐镱沐⒑耧⒑栳博┅┅ㄣ犰悱蝈箴镱箦蝈犰躞弪钺礤疳篌黠蜾躜铒钽濠戾è礤翳镤簌礅镬钺礤蟓ы弭栾洎┅戾è栳礓ㄣ镱汜躞弪钺礤⒑蝈犰⒑疳篌黠蜾┅ㄨ岵礓ㄣ镱汜礤翳镤⒑躜椹┅礓ㄣ镱汜栳⒑铒钽⒑栳博┅┅箦舡桢徜弪疳蜥眢箦翩蟓蝈聃弩舡桢徜弪螬扉篝ㄣ镱п豸栾蜷狒轱ㄣ镱т殓弩疳蜥眢┅┅┅鏖翳箪雉躞弪钺礤疳篌黠蜾箦戽戾è蝈犰ㄧ弭疳蜥Ⅱ遽祉┅铒钽ㄧ弭疳蜥㈩镱沐┅镳狁蹂ㄧ弭疳蜥镳狁蹂┅耧箴扉ㄧ弭疳蜥Ⅰ镳┅┅ㄣ镱è礤礅弪⑨豸琚耧呼弩＇羼踽飑戾è躜躜榄倔趄轭磲脲躜吼狒梵躜楫疳翳蟓躜飑厚蹂蜷弩躜楫聃弪殄蟓躜飑┅┅ㄣ铒钽蜥钿镯篝蜷铉┅钽鞍鞍氨耧⑨豸琚┅箦舡桢徜弪扉篝ㄣ镱Ⅴ箦蝾犴澧蟓躞弪钺礤┅ㄣ镱Ⅰ镳耧皓ㄣ镱Ⅴ蜷躜椹ㄣ镱㈩恽钽ㄣ镱泐镱沐泐镱沐ㄣ镱㈩镱沐铒钽濠ㄣ镱镳狁蹂镳狁蹂ㄣ镱Ⅱ遽祉蝈犰愆ㄣ镱Ⅱ弩痫铙澧ㄣ犰悱狨翳蝈箴镱箦蝈犰躞弪钺礤疳篌黠蜾躜铒钽钽耧泐镱沐┅┅┅è铛祆耧皓戾è躜躜榄倔趄轭磲脲躜吼狒梵躜楫疳翳蟓躜椹厚蹂蜷弩躜楫聃弪殄蟓躜椹┅┅ㄣ铒钽蜥钿镯篝蜷铉┅钽鞍鞍氨耧⑨豸琚┅箦舡桢徜弪扉篝ㄣ镱Ⅴ箦蝾犴澧蟓躞弪钺礤┅ㄣ镱Ⅰ镳耧皓ㄣ镱㈩恽钽ㄣ镱泐镱沐泐镱沐ㄣ镱㈩镱沐铒钽濠ㄣ镱镳狁蹂镳狁蹂ㄣ镱Ⅱ遽祉蝈犰愆ㄣ镱Ⅱ弩痫铙澧ㄣ犰悱蝈箴镱箦蝈犰躞弪钺礤疳篌黠蜾躜铒钽濠┅┅ㄨ趑甬溴怩箦戽⒄铙躔痫螋邃溟珏篝耧稷耧皓┅箦戽┅┅ㄤ彐礤翳镤梏麴磲脲蝈箴镱箦è箦戽梏麴蝈箴镱箦梏麴蝈箴镱箦┅鲠祯弩戾è孱糸糸弩ㄨ趑瓠蝈箴镱箦孱糸糸弩蝈箴镱箦┅ㄩ铛祆ㄣ潋孱糸糸弩┅ㄣ狎孱糸糸弩孱糸糸弩┅蝈箴镱箦┅ㄤ彐礤翳镤梏麴驽翥è箦戽梏麴镳糸镱犰蝈沲蝮舂ㄨ趑甬溴怩箦戽ф弭汨躜榄倔趄轭蟓躜飑┅戾舄è篝蝈犴ㄨ趑甬泔铑邈箦戽┅蝈聃弩ㄨ趑甬箦钿蝈聃弩箦戽篝蝈犴┅蝈箴镱箦ㄨ趑甬疳蝮瀛蝈箴镱箦箦戽篝蝈犴┅篝狒躞泔溴ㄨ趑瓠蝈箴镱箦篝狒躞泔溴蝈箴镱箦┅ㄤ邈灬蝈ㄩ珙矧蝈聃弩舂ㄣ镱è犷ㄥ窗篝狒躞泔溴ㄨ趑瓠蝈箴镱箦蝈箴镱箦桢徜弪蝈箴镱箦鼢鳝狨翳孱糸汜翦┅ㄨ趑甬溴怩箦戽躅狨翳矧辁邃躜榄倔趄轭蟓躜飑┅鏖翳箪雉躞弪钺礤疳篌黠蜾箦戽戾è桢徜弪ㄨ趑瓠蝈箴镱箦蝈箴镱箦桢徜弪蝈箴镱箦鼢鳝狨翳孱糸汜翦┅ㄣ镱换物躞弪钺礤疳篌黠蜾鼢鳝狨翳孱糸汜翦桢徜弪è矧铛祆躞弪钺礤铛祆疳篌黠蜾铛祆桢徜弪┅ㄣ祜箦篝蝈犴篝蝈犴ㄨ趑甬磲脲蝈箴镱箦箦戽蝈箴镱箦┅ㄣ祜箦篝蝈犴篝蝈犴ㄨ趑甬狨翳矧辁箦戽ㄣ狎桢徜弪ㄣ潋桢徜弪┅ㄨ趑甬驽翥箦戽┅┅┅è犷礤礅弪篝狒躞泔溴Ж嘲嘲嘲嘲嘲俯ㄨ趑瓠蝈箴镱箦蝈箴镱箦桢徜弪蝈箴镱箦ъ镢狒轱瞟铒ㄥ聃犰躜榄倔趄轭ㄨ趑瓠蝈箴镱箦珏舡蝈箴镱箦桢徜弪蝈箴镱箦ъ镢狒轱瞟躜榄倔趄轭蟓躜飑┅┅戾è祜汜糸镱ㄨ趑瓠蝈箴镱箦珏舡蝈箴镱箦桢徜弪蝈箴镱箦ъ镢狒轱瞟┅ㄨ趑甬溴怩箦戽蝈溟蝈泗轭绛麸祜汜糸镱ㄣ镱蝈沲蝮箦翩蟓躜飑祜汜糸镱ㄣ祜箦篝蝈犴篝蝈犴ㄨ趑甬驽翥箦戽铋飑ㄣ祜箦篝蝈犴篝蝈犴ㄨ趑甬磲脲蝈箴镱箦箦戽蝈箴镱箦┅┅ㄣ祜箦篝蝈犴篝蝈犴ㄨ趑甬磲脲蝈箴镱箦箦戽蝈箴镱箦┅┅ㄤ彐礤翳镤蝓衡彐矧è箦戽梏麴┅ㄨ趑甬箦趱瓠躜箦戽┅ㄤ彐礤翳镤梏麴弼犰踽翦è箦戽梏麴蝈篚祠蝈箴镱箦鲠祯弩蝈篚祠蝈箴镱箦┅ㄤ彐礤翳镤蝓横蝻躅è箦戽梏麴┅ㄩ矧铒ㄨ趑甬疳蝮瀛箦戽┅铒ㄨ趑甬弪蝻颦箦戽┅ㄣ犰飙铄舡礤翳镤箦戽眭祠轲戾鲠祯瀛忾钿蝈篚祠蝈箴镱箦ㄣ犰飙铄舡礤翳镤箦戽ㄨ趑甬弼犰踽翦箦戽蝈篚祠蝈箴镱箦┅┅ㄤ彐礤翳镤蝓è箦戽梏麴┅换戾è躜飙篝蜷铉躜榄倔趄轭蟓躜飑┅换ㄣ镱换è犷铒ㄥ蟓ы弭栾洎ц遽洎ㄨ趑甬汜汨瀛箦戽换ㄨ趑甬疳蝮瀛箦戽┅换ㄣ镱换è矧ㄨ趑甬溴怩箦戽铛祆ㄧ弭栳箬躜飙篝蜷铉梏麴汜汨瀚┅换眭祠轲戾鲠祯瀛忾钿ㄥ铘轸蝈箴镱箦ㄨ趑甬驽翥箦戽换箦翩ㄧ弭栳箬躜飙篝蜷铉梏麴汜汨瀚ㄣ镱孱糸豉蝈箴镱箦┅换鲠祯弩孱糸豉蝈箴镱箦┅换è羼蜥钿镯舶┅换眭祠轲戾鲠祯瀛忾钿ㄥ铘轸蝈箴镱箦ㄨ趑乎蜢躜飙篝蜷铉换吼矬舡溽翎蟓ю矬舡溽翎换喉弭栾ц遽换轰邂蹒ㄨ趑甬溴怩箦戽┅换ㄤ邈灬蝈ㄩ珙矧孱糸豉┅换戾è灬篝盹溟骈邃ㄨ趑瓠蝈箴镱箦珏舡孱糸豉桢徜弪换蝈箴镱箦ъ狍舡盹溟骈邃┅换ㄤ弩趄蹉趱蜷铉忾钿ㄥ铘轸蝈箴镱箦ㄧ弭栳箬躜飙篝蜷铉梏麴汜汨瀚换ㄣ镱换è灬篝盹溟骈邃ㄨ趑瓠蝈箴镱箦珏舡孱糸豉桢徜弪换蝈箴镱箦ъ狍舡盹溟骈邃┅换蝈龛狍躜飙篝蜷铉梏麴汜汨瀚换蝓箦戽┅换鲠祯弩孱糸豉蝈箴镱箦┅┅┅换换ㄤ弩趄蹉趱蜷铉忾钿ㄥ铘轸蝈箴镱箦ㄧ弭栳箬躜飙篝蜷铉梏麴汜汨瀚换鲠祯弩孱糸豉蝈箴镱箦┅┅换换ㄨ趑甬驽翥箦戽┅┅ㄨ趑甬驽翥箦戽┅ㄤ彐趄徙梏麴沆殄铘Ж蝓梏麴驽翥梏麴狨翳矧辁梏麴箦钿蝈聃弩梏麴疳蝮瀛蝈箴镱箦梏麴箦趱瓠躜梏麴弼犰踽翦梏麴溴怩绌换ㄤ弩趄蹉趱蜷铉忾钿筱桢礤蝈篝疳蜥礤翦蝮狨翳孱糸汜翦换ㄨ趑甬狨翳矧辁箦戽筱桢礤疳蜥礤翦蝮换ㄨ趑甬驽翥箦戽换ㄣ镱换è羼筱桢礤р狍殂换箩箝刘翳换ㄨ趑甬狨翳矧辁箦戽р狍殂疳蜥礤翦蝮换ㄣ祜箦篝蝈犴篝蝈犴换ㄨ趑甬驽翥箦戽┅换è羼筱桢礤т殓弩舂换ㄣ镱换è羼踽Ⅳ蝓澧换郁犰铒钽瀣蝈趄换ㄣ潋ㄡ篌镢Ⅲ翎戾疳蜥礤翦蝮呼弩＇羼踽飑┅换ㄣ祜箦篝蝈犴篝蝈犴换ㄨ趑甬驽翥箦戽┅换换ㄨ趑甬狨翳矧辁箦戽т殓弩疳蜥礤翦蝮换ㄣ祜箦篝蝈犴篝蝈犴换ㄨ趑甬驽翥箦戽┅┅换换ㄥ蝌矧箦戽⒄铍铒黝狨翳矧辁狒轱筱桢礤筱桢礤换鲠祯弩铋蝈箴镱箦┅┅